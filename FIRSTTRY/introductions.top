
topic: ~INTRODUCTIONS (~emogoodbye ~emohello ~emohowzit name here )


# Taucht jedes Mal auf.
t:  ( %input<%userfirstline )
    ^keep()
    [Hallo] [Hi] [Guten Tag], [ich bin Pret, das I steht für Intelligent] [ich bin ein freundlicher Bot]. ^reuse( GREET ) 

# Wird nur beim ersten Mal verwendet - noch in Arbeit.
# Drop table nur um mit build mine zu arbeiten.
t: GREET (~emohello) [Bevor ich dir helfe,] [Um persönlicher sein zu können,] würde ich gerne deinen Namen wissen. 

	#! Ich bin /Ich heiße / Meine Name ist
	a: VORSTELLUNG ([heiße bin ist lautet] _*1 >)
	    if ($cs_token == $stdtoken)
           {
           $cs_token = #DO_INTERJECTION_SPLITTING |
                       #DO_SUBSTITUTE_SYSTEM | #DO_NUMBER_MERGE |
                       #DO_PARSE
           retry(SENTENCE)
           }
        $kunde =  pos(noun '_0 proper)
        ^"Hallo, $kunde." ^reuse(~kaufabsicht.STARTKAUF)
        $cs_token = $stdtoken
        $userprompt = ^"$kunde: >"

	#! nur Name
	a: ( < _*1 > )
	   if ($cs_token == $stdtoken)
       {
       $cs_token = #DO_INTERJECTION_SPLITTING |
                   #DO_SUBSTITUTE_SYSTEM | #DO_NUMBER_MERGE |
                   #DO_PARSE
       retry(SENTENCE)
       }
       $kunde = pos(noun '_0 proper)
       ^"Hallo, $kunde." [Wie kann ich dir helfen] [Wobei brauchst du Hilfe] [Was kann ich für dich tun]?
       $cs_token = $stdtoken
       $userprompt = ^"$kunde: >"

	#! Das will ich nicht sagen.
	a: KEINE_VORSTELLUNG (<<[sagen] nicht>>) Das kann ich nachvollziehen - ist auch nicht weiter schlimm! Wie kann ich helfen?

	#! Das will ich nicht
	a: (<< [ich das] nicht>>) ^reuse(KEINE_VORSTELLUNG)

	#! Nein.
	a: (~no) ^reuse(KEINE_VORSTELLUNG)

	# Das hier noch überarbeiten!
	a: () Das habe ich leider nicht erkennen können. ^reuse( GREET )



# Bitte bei reuse nie ~ vergessen ^reuse( ~topicname.untertopic )